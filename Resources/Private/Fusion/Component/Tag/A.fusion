prototype(Webandco.Fusion:Component.Tag.A) < prototype(Webandco.Fusion:Component.Editable) {

    tagName = 'a'
    property = 'linkText'
    bemClass = 'link'
    bemModifier = Webandco.Fusion:BemModifier
    linkTarget = false

    linkText = 'must-be-set'
    linkHref = '#'
    linkEnabled = true

    @styleguide {
        title = 'Tag: link'
        props {
            linkText = 'link text'
            linkHref = '#'
        }
    }

    renderer {
        tagName = ${props.tagName}

        // assign href
        attributes.href = Webandco.Fusion:Component.Tag.A.Href {
            linkHref = ${props.linkHref}
            linkEnabled = ${props.linkEnabled}
        }

        // assign target if set
        attributes.target = ${props.linkTarget}
        attributes.target.@if.isset = ${(Type.isObject(node) && node.context.inBackend) == false || props.linkTarget != false}

        // assign content
        content = ${props.linkText}
    }
}

prototype(Webandco.Fusion:Component.Tag.A.Href) < prototype(Neos.Fusion:Component) {

    linkHref = 'must-be-set'
    linkEnabled = true

    renderer = Neos.Fusion:Case {

        inBackend {
            condition = ${props.linkEnabled && Type.isObject(node) && node.context.inBackend}
            renderer = ${'#inBackend'}
        }

        object {
            condition = ${Type.isObject(props.linkHref) && props.linkEnabled}
            renderer = Neos.Neos:NodeUri {
                node = ${props.linkHref}
            }
            renderer.@process.convertUris = Neos.Neos:ConvertUris {
                forceConversion = true
            }
        }

        node {
            condition = ${Type.isString(props.linkHref) && String.indexOf(props.linkHref, 'node')  >= 0 && props.linkEnabled}
            renderer = ${props.linkHref}
            renderer.@process.convertUris = Neos.Neos:ConvertUris {
                forceConversion = true
            }
        }

        resource {
            condition = ${String.indexOf(props.linkHref, 'resource') >= 0 && props.linkEnabled}
            renderer = Neos.Fusion:ResourceUri {
                path = ${props.linkHref}
            }
        }

        link {
            condition = ${String.indexOf(props.linkHref, 'http') >= 0}
            renderer = ${props.linkHref}
        }

        disabled {
            condition = ${!props.linkEnabled}
            renderer = ${'#disabled ' + props.linkHref}
        }

        default {
            condition = ${true}
            renderer = ${props.linkHref}
        }
    }
}

